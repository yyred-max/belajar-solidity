// SPDX-License-Identifier: GPL-3.0
pragma solidity >=0.7.0 <0.9.0;
///@title Voting with delegation.
contract Ballot {
    // This declares a new complex type which will 
    // be used for variable later
    // It will represent a single voter
    struct Voter {
        uint weight; //weight is accumulated by delegation
        bool voted; // if true, that person already voted
        address delegate; // person delegated to
        uint vote; //index of the voted proposal
    }

    // This is a type for a single proposal 
    struct Proposal {
        bytes32 name; //short name (up to 32 bythes)
        uint voteCount; // number of accumulates votes
    }

    address public chairperson;

    // This declares a state variable that 
    // stores a 'Voter' struct for each possible address
    mapping(address => Voter) public voters;

    // A dynamically-sized array of 'Proposal' structur 
    Proposal[] public proposals;

    /// Create a new ballot to choose one of 'proposalNames'
    constructor(bytes32[] memory proposalNames) {
        chairperson = msg.sender;
        voters[chairperson].weight = 1;

        // For each of the provided proposal names,
        // create a new proposal object and add id
        // to the end of the array
        for (uint i = 0; i < proposalNames.length; i++) {
            //'Proposal({...})' created a temporary
            // Proposal object and 'proposals.push (...)'
            // append it to the end of 'proposals'
            proposals.push(Proposal({
                name: proposalNames[i],
                voteCount: 0
            }));
        } 
    }

    // Give 'voter' the right to vote on this ballot
    // May only be called by 'chairperson'
    function giveRigthToVote(address voter) external {
        // If the first argument of 'require' evaluates
        // to 'false', execute terminates and all 
        // changes to the state and to Ether balances 
        // are reverted
        // This used to consume all gas in old EVM versions, but 
        // not anymore
        // It is aften a good idea to use 'require' to chech if 
        // fuctions are called correctly 
        // As a second argument, you can also provide an 
        // explanation about what went wrong 
        require (
            msg.sender == chairperson,
            "Only chairperson can give right to vote."
        );
        require(
            !voters[voter].voted,
            "The voter already voted."
        );
        require(voters[voter].weight == 0);
        voters[voter].weight = 1;
    }

    /// Delegate your vote to the voter 'to'
    function delegate(address to) external {
        // asigns reference 
        Voter storage sender = voters[msg.sender];
        require(sender.weight != 0, "You have no right to vote");
        require(!sender.voted, "You already voted.");

        require(to != msg.sender, "Self-delegation is disallowed.");

        // Forward the delegation as long as 
        // 'to' also delegated
        // In general, such Loops are very dengerous,
        // because if they run too Long, they might
        // need more gas than is available in a block 
        // In this case, the delegation will not be executed,
        // but in other situations, such Loops might
        // cause a contract to get "stuck" completely 
        while (voters[to].delegate != address(0)) {
            to = voters[to].delegate;

            // We found a Loop in the delegation, not allowed
            require(to != msg.sender, "Found loop in delegation.");
        }

        Voter storage delegate_ = voters[to];

        // Voters cannot delegate to accounts that cannot vote
        require(delegate_.weight >=1 );

        // Since 'sender' is a reference, this 
        // modifies 'voters[msg.sender]'
        sender.voted = true;
        sender.delegate = to;

        if (delegate_.voted) {
            // if the delegate already voted,
            // directly add to the number of votes
            proposals[delegate_.vote].voteCount += sender.weight;
        } else {
            // if the delegate did not vote yet,
            // add to her weight
            delegate_.weight += sender.weight;
        }
    }

    /// Give yout vote (including votes delegated to you)
    /// to proposal 'proposals[proposal].name'.
    function vote(uint proposal) external {
        Voter storage sender = voters[msg.sender];
        require(sender.weight !=0, "Has no right to vote");
        require(!sender.voted, "Already voted.");
        sender.voted = true;
        sender.vote = proposal;

        // If 'proposal' is out of the range of the array'
        // this will throw automatically and revert all 
        // changes
        proposals[proposal].voteCount += sender.weight;
    }

    /// @dev Computes the winning proposal taking all
    /// prevencious votes into account.
    function winningProposal() public view
        returns (uint winningProposal_) {
            uint winningVoteCount = 0;
            for (uint p = 0; p < proposals.length; p++) {
                if (proposals[p].voteCount > winningVoteCount) {
                    winningVoteCount = proposals[p].voteCount;
                    winningProposal_ = p;
                }
            }
        }

    // Calls winngingProposal() function to get the index
    // of the winner contained in the proposals array and then 
    // returns the name of the winner 
    function winnerName() external view 
            returns (bytes32 winnerName_) {
                winnerName_ = proposals[winningProposal()].name;
            }        
}